// A noise-generation algorithm that uses cellular automata.
// Based on this: https://www.reddit.com/r/proceduralgeneration/comments/95rtk1/i_came_up_with_a_noise_algorithm_that_ended_up/

//To use this algorithm, do the following:
//    1. Set u_PullDirs, u_Input, and u_Output, all of which should have the same dimensions.
//    2. Set some points in u_PointPixels/u_PointValues, and set u_NumbPoints accordingly.
//    3. Run the "Init" kernel.
//    4. Set u_MutationRange.
//    5. Set "NCompletedPixels" to be a 1-element array with the value 0.
//    6. While "NCompletedPixels[0]" is less than the total number of pixels:
//        a. Set "NCompletedPixels[0]" to 0.
//        b. Set u_Seed to a random value.
//        c. Run the "RunIteration" kernel.
//        d. Swap u_Input and u_Output.


#include "FloatHashers.cginc"


//----------- KERNEL DEFINITIONS ----------------

//Init sets up the initial automata state in u_Output,
//    and calculates/writes the data in u_PullDir.
#pragma kernel Init

//RunIteration runs an iteration of the cellular automata.
//It reads the current state from u_Input and writes it to u_Output.
#pragma kernel RunIteration


//----------- PARAMETERS/INPUTS/OUTPUTS -------------

//It is recommended to change this value every time a RunIteration kernel is run.
uniform float2 u_Seed;

#define MAX_POINTS 16
uniform uint u_NumbPoints = 0;
uniform uint2 u_PointPixels[MAX_POINTS];
uniform float u_PointValues[MAX_POINTS];

//A floating-point texture containing the random mutations that can be applied to a new cell value.
//The mutation is calculated by sampling this texture at a uniform-random pixel.
uniform Texture2D u_MutationRange;
uniform SamplerState sampler_u_MutationRange;

//The X is the horizontal direction to pull from (-1, 0, or +1).
//The Y is the vertical direction to pull from (-1, 0, or +1).
uniform RWTexture2D<int2> u_PullDirs;
//The X is the value (if set).
//The Y is 0 if not set, or 1 if set.
uniform RWTexture2D<float2> u_Input, u_Output;
//Should be set to a single value of 0 before running the kernel.
//Will be incremented once for every pixel that is set and no longer needs to be simulated.
uniform RWStructuredBuffer<uint> NCompletedPixels;


//---------- HELPER FUNCTIONS ----------------

float2 signF(float2 x, float epsilon)
{
    bool2 isTiny = abs(x) < epsilon;
    float2 normalSign = sign(x);
    return float2(isTiny.x ? 0.0 : normalSign.x,
                  isTiny.y ? 0.0 : normalSign.y);
}

float2 getPixelCenter(uint2 pixel)
{
    return float2(pixel.x + 0.5, pixel.y + 0.5);
}

//Finds the closest point.
//The Z component contains the distance to that point.
//Assumes u_NumbPoints is greater than 0.
float3 getClosestPointAndDist(float2 pixelPos)
{
    float2 closest = u_PointPixels[0];
    float closestDist = distance(pixelPos, closest);
    for (uint i = 1; i < u_NumbPoints; ++i)
    {
        float2 p = getPixelCenter(u_PointPixels[i]);
        float dist = distance(pixelPos, p);
        if (dist < closestDist)
        {
            closest = p;
            closestDist = dist;
        }
    }

    return float3(closest, closestDist);
}



//----------- KERNELS ------------

[numthreads(8,8,1)]
void Init(uint3 id : SV_DispatchThreadID)
{
    //----Set up pull dirs----

    int2 pullDirs;

    //Edge-case: there are no points.
    if (u_NumbPoints < 1)
    {
        pullDirs = 0;
    }
    else
    {
        //Get the closest point.
        float2 myP = getPixelCenter(id.xy);
        float3 closestPoint = getClosestPointAndDist(myP);

        //The first pull direction is the vertical delta to the point.
        //The second pull direction is the horizontal delta to the point.
        //Scale the pull directions based on how far they are.
        float2 toPoint = closestPoint.xy - myP;
        const float epsilon = 1.001;
        pullDirs = int2(signF(closestPoint.xy - myP, 0.01) * epsilon);
    }
    u_PullDirs[id.xy] = pullDirs;


    //----Set up cell data----

    float2 outData = float2(0.0, 0.0);
    for (uint i = 0; i < u_NumbPoints; ++i)
        if (id.x == u_PointPixels[i].x & id.y == u_PointPixels[i].y)
            outData = float2(u_PointValues[i], 1.0);
    u_Output[id.xy] = outData;
}

//Runs the cellular automata.
[numthreads(8,8,1)]
void RunIteration(uint3 id : SV_DispatchThreadID)
{
    uint2 texDim = 0;
    u_Output.GetDimensions(texDim.x, texDim.y);

    int2 pullDir = u_PullDirs[id.xy];
    float2 myState = u_Input[id.xy];

    //If this cell is already set, exit.
    if (myState.y > 0.5)
        return;

    //Get the state of the neighbors.
    //Note that according to DirectX, accessing a texture outside its bounds will return 0.
    float2 neighborStateX = u_Input[id.xy + uint2(pullDir.x, 0)],
           neighborStateY = u_Input[id.xy + uint2(0, pullDir.y)];

    //If one of the neighbors isn't set, exit.
    if (neighborStateX.y == 0.0 | neighborStateY.y == 0.0)
        return;

    //Calculate this cell's value.
    float baseValue = (neighborStateX.x + neighborStateY.x) / 2;
    float mutateT = hashTo1(float4(float2(id.xy), u_Seed)),
          mutateValue = u_MutationRange.SampleLevel(sampler_u_MutationRange,
                                                    float2(mutateT, 0.5),
                                                    0).x;
    float cellValue = baseValue + mutateValue;

    InterlockedAdd(NCompletedPixels[0], 1);
	u_Output[id.xy] = float2(cellValue, 1.0);
}
